<?xml version="1.0"?>
<s:HGroup xmlns:fx="http://ns.adobe.com/mxml/2009"
          xmlns:s="library://ns.adobe.com/flex/spark"
          verticalAlign="middle"
          initialize="init()"
          removedFromStage="timer.stop()">

    <fx:Script><![CDATA[
        import idv.cjcat.stardust.common.clocks.ImpulseClock;

        private var clock:ImpulseClock;
        private var timer:Timer = new Timer(1000,0);

        private function init() : void
        {
            timer.addEventListener(TimerEvent.TIMER, onTimer);
        }

        public function setData(d:ImpulseClock):void
        {
            clock = d;
            numParticlesTextInput.text = clock.impulseCount.toString();
            numBurstsTextInput.text = clock.repeatCount.toString();
            timer.start();
        }

        private function onTimer( e:TimerEvent ) : void
        {
            clock.impulse();
        }

        public function getData():ImpulseClock
        {
            return clock;
        }

        private function updateData():void
        {
            var numParticles:int = int(numParticlesTextInput.text);
            if (numParticles > 1000)
            {
                numParticles = 1000;
                numParticlesTextInput.text = numParticles.toString();
            }
            clock.impulseCount = numParticles;
            clock.repeatCount = int(numBurstsTextInput.text);
        }

        private function updateTimer():void
        {
            var burstInterval:int = int(bursIntervalTextInput.text);
            if (burstInterval < 20)
            {
                burstInterval = 20;
                bursIntervalTextInput.text = burstInterval.toString();
            }
            timer.delay = burstInterval;
            timer.reset();
            timer.start();
        }

    ]]></fx:Script>

    <s:Label text="bursts/impulse" />
    <s:TextInput id="numBurstsTextInput" restrict="0-9" change="updateData()" maxChars="2" width="50"/>

    <s:Label text="particles/impulse" />
    <s:TextInput id="numParticlesTextInput" restrict="0-9" change="updateData()" width="50"/>

    <s:Label text="burst interval(ms)" />
    <s:TextInput id="bursIntervalTextInput" restrict="0-9" text="1000" change="updateTimer()" width="50"/>
</s:HGroup>
