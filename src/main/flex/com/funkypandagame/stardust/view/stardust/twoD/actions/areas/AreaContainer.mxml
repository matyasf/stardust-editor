<?xml version="1.0"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009"
          xmlns:s="library://ns.adobe.com/flex/spark"
          removedFromStage="onRemovedFromStage()"
          addedToStage="onAddedToStage()"
          initialize="init()">

    <fx:Script><![CDATA[
        import com.funkypandagame.stardust.helpers.DropdownListVO;
        import com.funkypandagame.stardust.helpers.Globals;

        import idv.cjcat.stardustextended.actions.areas.Area;
        import idv.cjcat.stardustextended.actions.areas.IAreaContainer;

        import mx.collections.ArrayCollection;
        import mx.events.CollectionEvent;
        import mx.events.CollectionEventKind;

        private var itemWithArea:IAreaContainer;
        private var storedCoordinates:Vector.<Point> = new Vector.<Point>();
        private const areasAC:ArrayCollection = new ArrayCollection();
        private var stageRef:Stage;

        private function init():void {
            areasDropdownList.selectedIndex = 0;
            areasAC.addEventListener(CollectionEvent.COLLECTION_CHANGE, onAreasACChange);
        }

        private function onAreasACChange(e:CollectionEvent):void {
            if (e.kind != CollectionEventKind.UPDATE) {
                itemWithArea.areas = Vector.<Area>(areasAC.toArray());
            }
        }

        private function onAddedToStage():void {
            stage.addEventListener(MouseEvent.MOUSE_DOWN, onMouseDown);
            stageRef = stage;
        }

        private function onMouseDown(event:MouseEvent):void {
            if (followMouseCheckBox.selected) {
                if (event.target != followMouseCheckBox) {
                    followMouseCheckBox.selected = false;
                }
                removeEventListener(Event.ENTER_FRAME, handleFollowMouse);
                setAreaRendererOffset(0, 0);
            }
        }

        private function onRemovedFromStage():void {
            if (followMouseCheckBox.selected) {
                removeEventListener(Event.ENTER_FRAME, handleFollowMouse);
                setAreaRendererOffset(0, 0);
            }
            stageRef.removeEventListener(MouseEvent.MOUSE_DOWN, onMouseDown);
            stageRef = null;
        }

        public function setData(_itemWithArea:IAreaContainer):void {
            setAreaRendererOffset(0, 0);
            itemWithArea = _itemWithArea;
            const result:Array = [];
            for (var i:int = 0; i < _itemWithArea.areas.length; i++) {
                result.push(_itemWithArea.areas[i]);
            }
            areasAC.removeAll();
            dataGroup.validateProperties();  // flex bugfix
            areasAC.source = result;
        }

        private function followMouseChangeHandler(event:Event):void {
            if (followMouseCheckBox.selected) {
                storedCoordinates = new Vector.<Point>();
                for (var j:int = 0; j < areasAC.length; j++) {
                    var zone:Area = areasAC[j];
                    storedCoordinates.push(zone.getPosition());
                }
                addEventListener(Event.ENTER_FRAME, handleFollowMouse);
            }
        }

        private function handleFollowMouse(event:Event):void {
            setAreaRendererOffset(stage.mouseX - Globals.starlingCanvas.x, stage.mouseY - Globals.starlingCanvas.y);
        }

        private function setAreaRendererOffset(xc:Number, yc:Number):void {
            if (storedCoordinates.length > 0) {
                for (var j:int = 0; j < areasAC.length; j++) {
                    var area:Area = areasAC[j];
                    area.setPosition(storedCoordinates[j].x + xc, storedCoordinates[j].y + yc);
                }
            }
        }

        public function set followMouseVisible(val:Boolean):void {
            followMouseCheckBox.visible = followMouseCheckBox.includeInLayout = val;
        }

        private function addArea():void {
            var cl:Class = DropdownListVO(areasDropdownList.selectedItem).stardustClass;
            areasAC.addItem(new cl());
        }

        private static function ddlLabelFunc(item:Object):String {
            return DropdownListVO(item).name;
        }

        private static function areaIRFunction(item:Object):IFactory {
            return new ClassFactory(DropdownListVO(Globals.areasDict[item.constructor]).viewClass);
        }
        ]]></fx:Script>

    <s:HGroup width="100%" verticalAlign="middle">
        <s:Label text="Add new area:" />
        <s:DropDownList id="areasDropdownList"
                        width="160"
                        requireSelection="true"
                        dataProvider="{Globals.areasDDLAC}"
                        labelFunction="ddlLabelFunc"/>
        <s:Button label="Add area" click="addArea()"/>
        <s:CheckBox label="Follow mouse" id="followMouseCheckBox" change="followMouseChangeHandler(event)"
                    toolTip="This is just for testing in the editor, it will not be saved with the simulation!"/>
    </s:HGroup>

    <s:DataGroup width="100%" dataProvider="{areasAC}" itemRendererFunction="areaIRFunction" id="dataGroup">
        <s:layout>
            <s:VerticalLayout gap="3"/>
        </s:layout>
    </s:DataGroup>

</s:VGroup>
