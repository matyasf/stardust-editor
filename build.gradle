import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath group: 'org.gradlefx', name: 'gradlefx', version: '1.5.0'
    }
}

apply plugin: 'gradlefx'
apply plugin: 'ideafx'

group = 'com.funkypandagame'
version = '999'
type = 'swf'
srcDirs	= ['src/main/flex']
mainClass = 'com/funkypandagame/stardust/Stardusttool.mxml'
frameworkLinkage = 'merged'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://raw.githubusercontent.com/tconkling/maven-repo/master/"
    }
    ivy {
        name 'Apache'
        artifactPattern 'http://archive.apache.org/dist/flex/[revision]/binaries/[module]-[revision]-bin.[ext]'
    }
    ivy {
        name 'Adobe Air SDK'
        artifactPattern  Os.isFamily(Os.FAMILY_WINDOWS) ?
                'http://airdownload.adobe.com/air/win/download/[revision]/[module]_Compiler.[ext]' :
                'http://airdownload.adobe.com/air/mac/download/[revision]/[module]_Compiler.[ext]'
    }
}

dependencies {
    flexSDK files('C:/PROGRAMS/Flex_4_16_AIR20.zip') // sadly GradleFX's auto installer is broken :(
    //flexSDK group: 'org.apache', name: 'apache-flex-sdk', version: '4.16.0', ext: 'tar.gz'
    //airSDK group: 'com.adobe', name: 'AIRSDK', version: '20.0', ext: Os.isFamily(Os.FAMILY_WINDOWS) ? 'zip' : 'tbz2'
    
    merged files('libs/robotlegs-framework-v2.2.1.swc')
    merged files('libs/as3-signals-0.8.swc')

    merged 'com.funkypandagame:stardust-sim-loader:3.31'
}

additionalCompilerOptions = [
    // AS3 metadata
    '-keep-as3-metadata+=Inline,Embed,SWF,Transient',
    // Flex metadata
    '-keep-as3-metadata+=Alternative,ArrayElementType,Bindable,DefaultProperty,Deprecated,Effect,Event,' +
                        'HostComponent,IconFile,Inspectable,InstanceType,NonCommittingChangeEvent,RemoteClass,' +
                        'RichTextContent,SkinPart,SkinState,Style',
    // Robotlegs metadata
    '-keep-as3-metadata+=Inject,PreDestroy,PostConstruct',
    '-advanced-telemetry'
]

sdkAutoInstall {
    showPrompts = false
}

///////////////////////////////////////// wrapper
task wrapper(type: Wrapper) {
    group = 'Buildmaster'
    description = 'Generates gradlew and gradlew.bat bootstrap scripts.'
    gradleVersion = '3.2.1'
    // place jar file and properties into a subdirectory to avoid root dir clutter
    jarFile = 'gradle/wrapper/gradle.jar'
}